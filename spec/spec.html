<!DOCTYPE html>
<title>Depict spectrogram of an audio file</title>
<script src=UFXgl.js></script>
<script src=UFXmaximize.js></script>
<script type="x-shader/x-vertex" id="vfill">
uniform highp vec2 viewportsize;
void main() {
	gl_Position = vec4(0.0, 0.0, 0.0, 1.0);
	gl_PointSize = max(viewportsize.x, viewportsize.y);
}
</script>
<script type="x-shader/x-vertex" id="fdump">
uniform sampler2D texture;
uniform highp vec2 viewportsize;
void main() {
	gl_FragColor = vec4(texture2D(texture, gl_FragCoord.xy / viewportsize).rgb, 1.0);
}
</script>
<script type="x-shader/x-vertex" id="fchunk">
precision highp float;
uniform sampler2D rawscape;
uniform vec2 viewportsize, rawscapesize;
uniform float hmin, hmax;
uniform float F;
const float tau = 6.283185307179586;

const int C = 128;

float blackman(in float g) {
	return 21.0 - 25.0 * cos(tau * g) + 4.0 * cos(2.0 * tau * g);
}
float wmax = blackman(0.5);

vec2 toshort(in float x) {
	x = clamp(floor((x + 1.0) / 2.0 * 65535.0), 0.0, 65535.0);
	float a = floor(x / 256.0);
	return vec2(x - 256.0 * a, a) / 255.0;
}

void main() {
	float h = hmin + (hmax - hmin) * gl_FragCoord.y / viewportsize.y;
	float f = 440.0 * exp(h * log(2.0) / 12.0);
	float jchunk = gl_FragCoord.x;
	float W = viewportsize.x * viewportsize.y;
	vec2 z;
	for (int i = 0; i < C; ++i) {
		float m = float(i) + float(C) * jchunk;
		float w = blackman((m + 1.0) / (W + 1.0));
		vec2 rpos = vec2(float(i), jchunk) / rawscapesize;
		float v = texture2D(rawscape, rpos).r * 2.0 - 1.0;
		float phi = m * f / F * tau;
		z.x += v * w * cos(phi);
		z.y += v * w * sin(phi);
	}
	z /= float(C) * wmax;
	gl_FragColor.rg = toshort(z.x);
	gl_FragColor.ba = toshort(z.y);
}
</script>
<script type="x-shader/x-vertex" id="fspec">
precision highp float;
uniform sampler2D chunkscape;
uniform vec2 viewportsize, chunkscapesize;

const int P = 128;

float fromshort(in vec2 a) {
	float x = 255.0 * (a.x + 256.0 * a.y);
	return (x / 65535.0) * 2.0 - 1.0;
}

void main() {
	float y = gl_FragCoord.y;
	vec2 z;
	for (int i = 0; i < P; ++i) {
		vec2 cpos = vec2(float(i), y) / chunkscapesize;
		vec4 s = texture2D(chunkscape, cpos);
		z.x += fromshort(s.rg);
		z.y += fromshort(s.ba);
	}
	z /= float(P);
	float a = length(z);
	a = log(a) * 0.2 + 1.0;
	gl_FragColor = vec4(vec3(a), 1.0);
}
</script>
<script src=util.js></script>
<body>
<canvas id=canvas></canvas>
</body>
<script>
init()
//UFX.maximize.fill(canvas, "total")
UFX.maximize.onadjust = function (x, y) {
	gl.resize(x, y)
	gl.clearColor(0.1, 0.1, 0.1, 1)
	gl.clear(gl.COLOR_BUFFER_BIT)
}
gl.clearColor(0.1, 0.1, 0.1, 1)
gl.clear(gl.COLOR_BUFFER_BIT)

function cancel(e) {
	e.preventDefault()
	return false
}
canvas.addEventListener("dragover", cancel, false)
canvas.addEventListener("dragenter", cancel, false)
canvas.addEventListener("drop", function (e) {
	handleupload(e.dataTransfer)
	return cancel(e)
}, false)

</script>

